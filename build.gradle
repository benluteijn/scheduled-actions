/*
 * Copyright 2015 Netflix, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
    repositories {
        jcenter()
        maven { url 'https://spinnaker.bintray.com/gradle/' } 
    }
    dependencies {
        classpath 'com.netflix.spinnaker.gradle:spinnaker-gradle-project:3.9.0'
    }
}



allprojects {
    apply plugin: 'spinnaker.project'
    apply plugin: "groovy"
    apply plugin: 'idea'
    apply plugin: 'eclipse'
    group = "com.netflix.scheduledactions"

    ext {
        spinnakerDependenciesVersion = project.hasProperty('spinnakerDependenciesVersion') ? project.property('spinnakerDependenciesVersion') : '0.63.0'
    }

    def checkLocalVersions = [spinnakerDependenciesVersion: spinnakerDependenciesVersion]
    if (ext.has('versions')) {
        def extVers = ext.get('versions')
        if (extVers instanceof Map) {
            checkLocalVersions.putAll(extVers)
        }
    }

    def localVersions = checkLocalVersions.findAll { it.value.endsWith('-SNAPSHOT') }
    if (localVersions) {
        logger.info("Enabling mavenLocal repo for $localVersions")
        repositories {
            mavenLocal()
        }
    }

    spinnaker {
        dependenciesVersion = spinnakerDependenciesVersion
    }
}

subprojects {
    apply plugin: 'groovy'

    dependencies {
        compile spinnaker.dependency('groovy')
        compile spinnaker.dependency('slf4jApi')
        compile 'net.redhogs.cronparser:cron-parser-core:2.8'
        spinnaker.group('spockBase')
        spinnaker.group('test')
        testCompile spinnaker.dependency('slf4jSimple')
        testCompile spinnaker.dependency('groovy')
    }

    tasks.compileGroovy.enabled = false
    tasks.javadoc.failOnError = false
}
